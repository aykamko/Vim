#
# Executes commands at login post-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Execute code that does not affect the current session in the background.
{
  # Compile the completion dump to increase startup speed.
  zcompdump="${ZDOTDIR:-$HOME}/.zcompdump"
  if [[ -s "$zcompdump" && (! -s "${zcompdump}.zwc" || "$zcompdump" -nt "${zcompdump}.zwc") ]]; then
    zcompile "$zcompdump"
  fi
} &!

# Print a random, hopefully interesting, adage.
if (( $+commands[fortune] )); then
  if [[ -t 0 || -t 1 ]]; then
    fortune -s
    print
  fi
fi

if [[ -o LOGIN ]]; then
  {
    local yellow="\x1b[33m"
    local reset="\x1b[0m"
    echo "${yellow}Started zsh in about ${$(( $EPOCHREALTIME - $ZSH_START_TIME )):0:5} seconds.${reset}"
  }
fi

if [[ $ZSH_PROFILE_STARTUP -eq 1 ]]; then
  unsetopt xtrace
  exec 2>&3 3>&-
  if [ -f $DOTFILES/bin/zsh-profile-processor.py ]; then
    $DOTFILES/bin/zsh-profile-processor.py $ZSH_START_TIME $ZSH_PROFILER_LOG
  fi
fi

###############################################################################
# Autostart TMUX
###############################################################################
if [[ -f "$HOME/.no_tmux" ]]; then
  builtin cd $(cat $HOME/.no_tmux)
  command rm -f $HOME/.no_tmux
elif [[ -z "$TMUX" ]]; then
  if [[ -f "$HOME/.tmux_new" ]]; then
    command rm -f $HOME/.tmux_new
    tmux new
  else
    tmux attach || sleep 0.01; tmux
  fi
fi
