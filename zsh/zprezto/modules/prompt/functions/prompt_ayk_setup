# vim: set ft=zsh:
#
# A cool theme, yo.
#
# Authors:
#   Aleks Kamko <aykamko@gmail.com>
#

_c1='cyan'
_c2='blue'
_c3='magenta'
_c4='red'
_hide='240'
_attn='14'

# Get the commit difference counts between local and remote.
function +vi-git_ahead_behind {
    local remote_info ahead_and_behind_cmd ahead_and_behind ahead behind
    ahead_and_behind_cmd='git rev-list --count --left-right HEAD...@{upstream}'
    ahead_and_behind="$(${(z)ahead_and_behind_cmd} 2> /dev/null)"
    ahead="$ahead_and_behind[(w)1]"
    behind="$ahead_and_behind[(w)2]"

    if (( ahead > 0 )); then
        remote_info+="+$ahead"
    fi
    if (( behind > 0 )); then
        (( ahead > 0 )) && remote_info+=", "
        remote_info+="-$behind"
    fi
    [[ -n $remote_info ]] && hook_com[misc]="%F{$_c2}{%f%F{yellow}$remote_info%f%F{$_c2}}%f"
    return 0
}

function +vi-git_changes_fmt {
    if [[ -n ${hook_com[staged]} ]]; then
        hook_com[staged]=" ${hook_com[staged]}"
    elif [[ -n ${hook_com[unstaged]} ]]; then
        hook_com[unstaged]=" ${hook_com[unstaged]}"
    fi
    return 0
}

export VIRTUAL_ENV_DISABLE_PROMPT=1
function virtualenv_info {
    if [[ -n ${VIRTUAL_ENV} ]]; then
        _venv="%F{$_c2}[%füêç %F{white}${VIRTUAL_ENV:t}%f%F{$_c2}]%f"
    else
        unset _venv
    fi
}

# xterm: set pane title
function xterm_title {
  # execute in background
  echo -ne "\033]2;$(egrep -o '[^/]*(\/[^/]*(\/[^/]*)?)?$' <<< "${PWD/#$HOME/~}")\033\\" &!
}

# docker: show marker if machine is mounted in env
function docker_mounted {
    if [[ -n ${DOCKER_MACHINE_NAME} ]]; then
        local machine_name
        [[ "${DOCKER_MACHINE_NAME}" = "default" ]] && unset machine_name || machine_name="$DOCKER_MACHINE_NAME"
        _docker="%F{$_c2}[%füê≥ %F{white}${machine_name}%f%F{$_c2}]%f"
    else
        unset _docker
    fi
}

# PWD: Add space before prompt extras, if they exist.
function prompt_ayk_pwd {
    _pwd="%F{$_c1}%3~%f"

    local prompt_extras
    prompt_extras=(
        $vcs_info_msg_0_
        $_venv
        $_docker
    )
    [[ ${#prompt_extras[@]} -gt 0 ]] && _pwd+=" "
}

function prompt_ayk_precmd {
    local prompt_start_time=$EPOCHREALTIME

    vcs_info
    virtualenv_info
    docker_mounted
    xterm_title
    prompt_ayk_pwd

    _prompt_time=${$(( ($EPOCHREALTIME - $prompt_start_time) * 1000 ))%%\.*}
}

function prompt_ayk_setup {
    setopt LOCAL_OPTIONS
    unsetopt XTRACE KSH_ARRAYS
    prompt_opts=(cr percent subst)

    # Load required functions.
    autoload -Uz add-zsh-hook
    autoload -Uz vcs_info

    # Add hook for calling vcs_info before each command.
    add-zsh-hook precmd prompt_ayk_precmd

    # If terminal is interactive non-login (AKA neovim's libvterm), change prompt emoji
    [[ -o INTERACTIVE && ! -o LOGIN ]] && prompt_emoji="üëæ " || prompt_emoji="üêº "

    # If terminal not in tmux, prepend message
    [[ -z "$TMUX" ]] && prompt_tmux="%F{$_c3}[--tmux]%f " || prompt_tmux=""

    # Set editor-info parameters.
    zstyle ':prezto:module:editor:info:completing' format "%B%F{$_c4}...%f%b"
    zstyle ':prezto:module:editor:info:keymap:primary' format "${prompt_emoji}"
    zstyle ':prezto:module:editor:info:keymap:alternate' format "üê∑ %F{$_attn}"
    zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format "%B%F{$_c4}!%f%b"

    # Set vcs_info parameters.
    zstyle ':vcs_info:*' enable git
    zstyle ':vcs_info:*' check-for-changes true
    zstyle ':vcs_info:*' stagedstr "%F{green}‚óè%f"
    zstyle ':vcs_info:*' unstagedstr "%F{yellow}‚óè%f"
    zstyle ':vcs_info:git:*' formats "%F{$_c2}(%f%F{$_c4}%b%f%c%u%F{$_c2})%f%m"
    zstyle ':vcs_info:git:*' actionformats "%F{$_c2}(%f%F{$_c4}%b%f%c%u%F{$_c2}|%f%F{$_c1}%a%f%F{$_c2})%f%m"
    zstyle ':vcs_info:*' formats "%F{$_c2}(%f%F{$_c4}%b%f%c%u%F{$_c2})%f"
    zstyle ':vcs_info:*' actionformats "%F{$_c2}(%f%F{$_c4}%b%f%c%u%F{$_c2}|%f%F{$_c1}%a%f%F{$_c2})%f"
    zstyle ':vcs_info:git*+set-message:*' hooks git_ahead_behind git_changes_fmt

    # Define prompts.
    PROMPT='${prompt_tmux}${_pwd}${_docker}${_venv}${vcs_info_msg_0_} ${editor_info[keymap]}${editor_info[overwrite]} '
    RPROMPT='%f%(?::%F{yellow}‚èé%f) %F{$_hide}[${_prompt_time}]%f'
}

prompt_ayk_setup "$@"
