# vim: set ft=zsh:ts=2:sts=2:sw=2:
###############################################################################
# Misc
###############################################################################
export UNAME=${$(uname -s):l}
export DOTFILES=$HOME/dotfiles
export WORKON_HOME=$HOME/.pyvirtualenvs # Virtualenv
export TEXMFHOME=texmf # Latex
export KEYTIMEOUT=1 # Reduce command line vim delay

###############################################################################
# Browser
###############################################################################
if [[ $UNAME == darwin ]]; then
  export BROWSER='open'
fi

###############################################################################
# Editors
###############################################################################
if (( $+commands[nvim] )); then
  if (( $+commands[nvim-client] )); then
    export EDITOR='nvim-client'
  else
    export EDITOR='nvim'
  fi
else
  export EDITOR='vim'
fi
export VISUAL='vim'
export PAGER='less'

###############################################################################
# Language
###############################################################################
export LC_ALL='en_US.UTF-8'
export LANG='en_US.UTF-8'
export LANGUAGE='en_US.UTF-8'

###############################################################################
# Less
###############################################################################
# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

###############################################################################
# Paths
###############################################################################
# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

if [[ $UNAME == darwin ]]; then
  # used by Homebrew
  path=(/usr/local/sbin $path)
fi

if (( $+commands[go] )); then
  path+=(/usr/local/opt/go/libexec/bin)
fi

if [[ -d $HOME/.pyenv ]]; then
  export PYENV_SHIMS=$HOME/.pyenv/shims
  path=($PYENV_SHIMS $path)
fi

if [[ -d $HOME/.rbenv ]]; then
  export RBENV_SHIMS=$HOME/.rbenv/shims
  path=($RBENV_SHIMS $path)
fi

if [[ -d $HOME/.nodenv ]]; then
  export NODENV_SHIMS=$HOME/.nodenv/shims
  path=($NODENV_SHIMS $path)
fi

if [[ -d $DOTFILES/bin ]]; then
  path+=($DOTFILES/bin)
  [[ -d $DOTFILES/bin/$UNAME ]] && path+=($DOTFILES/bin/$UNAME)
fi

###############################################################################
# Mosh
###############################################################################
export MOSH_PID="$(ps ux | grep '[m]osh-server' 2>/dev/null | awk '{print $2}')"
